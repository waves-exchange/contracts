{-# STDLIB_VERSION 7 #-}
{-# CONTENT_TYPE DAPP #-}
{-# SCRIPT_TYPE ACCOUNT #-}

let kFactoryPublicKey = "%s__factoryPublicKey"
let kServicePublicKey = "%s__servicePublicKey"
let kOwnerPublicKey = "%s__ownerPublicKey"
let kBotPublicKey = "%s__botPublicKey"
let kVerified = "%s__verified"

let factoryAddress = addressFromPublicKey(this.getBinaryValue(kFactoryPublicKey))
let serviceAddress = addressFromPublicKey(factoryAddress.getBinaryValue(kServicePublicKey))
let ownerAddress = addressFromPublicKey(this.getBinaryValue(kOwnerPublicKey))

let isReady = this.getBinary(kFactoryPublicKey).isDefined()
  && this.getBinary(kOwnerPublicKey).isDefined()
  && this.getBoolean(kVerified).valueOrElse(false)

func mustAddress(i: Invocation, address: Address) = {
  i.caller == address || throw()
}

func mustThis(i: Invocation) = {
  mustAddress(i, this)
}

func mustService(i: Invocation) = {
  mustAddress(i, serviceAddress)
}

func mustOwner(i: Invocation) = {
  mustAddress(i, ownerAddress)
}

func mustFactory(i: Invocation) = {
  mustAddress(i, factoryAddress)
}

@Callable(i)
func stringEntry(key: String, val: String) =
  if (i.mustService()) then ([StringEntry(key, val)], key) else ([], unit)

@Callable(i)
func integerEntry(key: String, val: Int) =
  if (i.mustService()) then ([IntegerEntry(key, val)], key) else ([], unit)

@Callable(i)
func transferAsset(recipientBytes: ByteVector, amount: Int, assetId: ByteVector) = 
  if (i.mustService()) then ([ScriptTransfer(Address(recipientBytes), amount, assetId)], amount) else ([], unit)

@Callable(i)
func transferWaves(recipientBytes: ByteVector, amount: Int) = 
  if (i.mustService()) then ([ScriptTransfer(Address(recipientBytes), amount, unit)], amount) else ([], unit)

@Callable(i)
func init(factoryPublicKey: ByteVector, ownerPublicKey: ByteVector) = {
  strict checkCaller = i.mustThis()

  ([
    BinaryEntry(kFactoryPublicKey, factoryPublicKey),
    BinaryEntry(kOwnerPublicKey, ownerPublicKey)
  ], unit)
}

@Callable(i)
func complete(requestId: String, recipientPublicKey: ByteVector) = {
  strict checkCaller = i.mustThis()
  let result = factoryAddress.reentrantInvoke("complete", [requestId, recipientPublicKey], [])

  (nil, result)
}

# called by factory to close the contract
@Callable(i)
func approve() = {
  strict checkCaller = i.mustFactory()

  ([
    BooleanEntry(kVerified, true)
  ], unit)
}

# owner can call only this function
# service address should be replaced to change logic
@Callable(i)
func call(function: String, args: List[String]) = {
  strict checkCaller = i.mustOwner()
  let result = serviceAddress.reentrantInvoke(function, [args], i.payments)

  (nil, result)
}

# bot can trade
@Verifier(tx)
func verify() = {
  let botPublicKey = factoryAddress.getBinaryValue(kBotPublicKey)
  match tx {
    case _: Order => sigVerify(tx.bodyBytes, tx.proofs[0], botPublicKey)
    case _: InvokeScriptTransaction => if (isReady) then throw() else {
      sigVerify(tx.bodyBytes, tx.proofs[0], tx.senderPublicKey)
    }
    case _ => throw()
  }
}
